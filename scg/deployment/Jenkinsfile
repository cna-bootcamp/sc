@Library('pipeline_shared_library')

def SRC_DIR = "scg"
def PIPELINE_ID = "${SRC_DIR}-${env.BUILD_NUMBER}"

stage("Prepare Environment") {
    podTemplate(
        label: "${PIPELINE_ID}",
        containers: [
            containerTemplate(name: 'ssh', image: 'kroniak/ssh-client', command: 'cat', ttyEnabled: true),
        ]
    )
    {
        //-- NFS 공유 볼륨 디렉토리 생성
        node("${PIPELINE_ID}") {
            stage("Prepare Cache Volumes") {
                container("ssh") {
                    withCredentials([sshUserPrivateKey(credentialsId: "${NFS_CREDENTIAL}", keyFileVariable: 'SSH_KEY_FILE', usernameVariable: 'SSH_USER')]) {
                        sh """
                            mkdir -p ~/.ssh
                            ssh-keyscan -H ${NFS_HOST} >> ~/.ssh/known_hosts

                            ssh -i ${SSH_KEY_FILE} ${SSH_USER}@${NFS_HOST} "mkdir -p /${NFS_DIR}/${GRADLE_CACHE_DIR}/${SRC_DIR}"
                            ssh -i ${SSH_KEY_FILE} ${SSH_USER}@${NFS_HOST} "mkdir -p /${NFS_DIR}/${TRIVY_CACHE_DIR}/${SRC_DIR}"
                        """
                    }
                }
            }
        }
    }
}

stage("Build and Deploy") {
    podTemplate(
        label: "${PIPELINE_ID}",
        containers: [
            containerTemplate(name: "trivy", image: "aquasec/trivy", ttyEnabled: true, command: "cat"),
            containerTemplate(name: "kubectl", image: "lachlanevenson/k8s-kubectl", command: "cat", ttyEnabled: true),
            containerTemplate(name: "gradle", image: "gradle:jdk17", ttyEnabled: true, command: "cat"),
            containerTemplate(name: 'podman', image: "mgoltzsche/podman", ttyEnabled: true, command: 'cat', privileged: true),
            containerTemplate(name: 'envsubst', image: "hiondal/envsubst", command: 'sleep', args: '1h')
        ],
        volumes: [
            nfsVolume( mountPath: "/home/gradle/.gradle", serverAddress: "${NFS_HOST}", serverPath: "/${NFS_DIR}/${GRADLE_CACHE_DIR}/${SRC_DIR}", readOnly: false ),
            nfsVolume(mountPath: "/${TRIVY_CACHE_DIR}", serverAddress: "${NFS_HOST}", serverPath: "/${NFS_DIR}/${TRIVY_CACHE_DIR}/${SRC_DIR}", readOnly: false)
        ]
    )
    {
        node("${PIPELINE_ID}") {
            stage("Get Source") {
                checkout scm
            }

            //---- 소스 변경 여부 검사
            def hasChangesInConfig = commonFunctions.checkChangesInDirectory("${SRC_DIR}")

            if (!hasChangesInConfig) {
                echo "No changes in <${SRC_DIR}> directory. Skipping pipeline."
                currentBuild.result = 'SUCCESS'
                //commonFunctions.notifySlack("SKIPPED", "#00FF00")
                return
            }
            //------------------------------

            commonFunctions.notifySlack("STARTED", "#FFFF00")

            baseDir = "${SRC_DIR}/deployment"
            def props = readProperties  file:"${baseDir}/deploy_env_vars"
            applicationName = props["application_name"]
            artifactoryFile = props["artifactory_file"]
            tag = commonFunctions.getTimestamp()
            namespace = props["namespace"]
            manifest = props["manifest"]
            imageScanSeverity = props["image_scan_severity"]
            sonarProjectKey = props["sonar_project_key"]

            def imagePath = "${IMAGE_REG_HOST}/${IMAGE_REG_ORG}/${applicationName}"
            def eurekaServiceUrl = commonFunctions.getEurekaServiceUrl()

            try {
                stage("Build Jar") {
                    container("gradle") {
                        sh 'echo "Build jar under build directory"'
                        sh "gradle :${SRC_DIR}:build -x test"
                    }
                }

                stage("SonarQube Analysis") {
                    container("gradle") {
                        withSonarQubeEnv("${SONAR_SERVER_ID}") {
                            sh """
                                gradle :${applicationName}:sonar \
                                    -Dsonar.projectName=${sonarProjectKey} \
                                    -Dsonar.projectKey=${sonarProjectKey} \
                                    -Dsonar.java.binaries=${JAVA_BINARY_DIR}
                            """
                        }
                    }
                }

                stage("SonarQube Quality Gate") {
                    timeout(time: 10, unit: 'MINUTES') {
                        def qg = waitForQualityGate()
                        if (qg.status != 'OK') {
                            echo "SonarQube Quality Gate failed. Aborting the pipeline."
                            currentBuild.result = 'FAILURE'
                            commonFunctions.notifySlack("SonarQube Quality Gate failed.", "#FF0000")
                            error "Pipeline aborted due to SonarQube Quality Gate failure: ${qg.status}"
                        }
                    }
                }

                stage("Build Container image") {
                    container("podman") {
                        withCredentials([usernamePassword(
                            credentialsId: "${IMAGE_REG_CREDENTIAL}",
                            usernameVariable: 'USER',
                            passwordVariable: 'PASSWORD'
                        )]) {
                            sh """
                                echo user "$USER" pasword "$PASSWORD"
                                podman login ${IMAGE_REG_HOST} --username ${USER} --password ${PASSWORD}
                                podman build -f ${baseDir}/Dockerfile \
                                    -t ${imagePath}:${tag} \
                                    --build-arg BUILD_LIB_DIR=${BUILD_LIB_DIR} \
                                    --build-arg ARTIFACTORY_FILE=${artifactoryFile} \
                                     ${SRC_DIR}
                                podman push ${imagePath}:${tag}

                                podman tag ${imagePath}:${tag} ${imagePath}:latest
                                podman push ${imagePath}:latest
                            """
                        }
                    }
                }

                stage("Scan Image Vulnerability") {
                    timeout(time: 10, unit: 'MINUTES') {
                        container("trivy") {
                            def trivyOutput = sh (
                                script: """
                                    trivy image \
                                        --cache-dir /${TRIVY_CACHE_DIR} \
                                        --scanners vuln \
                                        --severity ${imageScanSeverity} \
                                        --exit-code 0 \
                                        ${imagePath}:${tag}
                                """,
                                returnStdout: true
                            ).trim()

                            echo "Trivy scan results:"
                            echo trivyOutput

                            def vulnerabilityCounts = commonFunctions.getVulnerabilityResult(trivyOutput)

                            if (vulnerabilityCounts["CRITICAL"] > 0) {
                                echo "Critical vulnerabilities found. Aborting the pipeline."
                                currentBuild.result = 'FAILURE'
                                commonFunctions.notifySlack("Vulnerability scan failed.", "#FF0000")
                                error "Critical vulnerabilities found."
                            } else {
                                echo "No critical vulnerabilities found. Continuing the pipeline."
                            }
                        }
                    }
                }

                stage( "Generate deployment yaml" ) {
                    container("envsubst") {
                        sh """
                            set -a
                            source ${baseDir}/deploy_env_vars
                            set +a

                            export tag=${tag}
                            export image_pull_secret=${IMAGE_REG_PULL_SECRET}
                            export image_pull_policy=${IMAGE_PULL_POLICY}
                            export image_path=${imagePath}:${tag}
                            export eureka_service_url=${eurekaServiceUrl}

                            envsubst < ${baseDir}/${manifest}.template > ${baseDir}/${manifest}
                            cat ${baseDir}/${manifest}
                        """
                    }
                }

                stage( "Deploy to Cluster" ) {
                    container("kubectl") {
                        sh "kubectl apply -f ${baseDir}/${manifest} -n ${namespace}"
                    }
                }

                commonFunctions.notifySlack("${currentBuild.currentResult}", "#00FF00")
                echo "**** FINISH ALL STAGES : SUCCESS"
            } catch(e) {
                currentBuild.result = "FAILED"
                commonFunctions.notifySlack("${currentBuild.currentResult}", "#FF0000")
            }
        }
    }
}